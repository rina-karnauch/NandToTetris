// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/Memory.hdl

/**
 * The complete address space of the Hack computer's memory,
 * including RAM and memory-mapped I/O. 
 * The chip facilitates read and write operations, as follows:
 *     Read:  out(t) = Memory[address(t)](t)
 *     Write: if load(t-1) then Memory[address(t-1)](t) = in(t-1)
 * In words: the chip always outputs the value stored at the memory 
 * location specified by address. If load==1, the in value is loaded 
 * into the memory location specified by address. This value becomes 
 * available through the out output from the next time step onward.
 * Address space rules:
 * Only the upper 16K+8K+1 words of the Memory chip are used. 
 * Access to address>0x6000 is invalid. Access to any address in 
 * the range 0x4000-0x5FFF results in accessing the screen memory 
 * map. Access to address 0x6000 results in accessing the keyboard 
 * memory map. The behavior in these addresses is described in the 
 * Screen and Keyboard chip specifications given in the book.
 */

CHIP Memory {
    IN in[16], load, address[15];
    OUT out[16];

    PARTS:
    // 2^14 = 16384, 2^13 = 8192, 2^14+2^13 = 24576
    // looking on in address[13-14]:
    // 00 or 01 is below address 16384 -> output into RAM
    // 10 is above 16384 -> output into screen
    // 11 is above 24576-> output keyboard
    DMux4Way(in=load, sel=address[13..14], a=toRAMa , b=toRAMb, c=toScreen, d=toKeyboard);
    // if we have in address[13..14] 01 or 00 it doesn't matter, we still output into ram.
    // so a Or b indicated when we output into ram, if we pick only one it might be 0 even if the other is 1
    // so we add them up.
    Or(a=toRAMa, b=toRAMb, out=toRAM);


    // ram part of the memory:
    RAM16K(in=in, load=toRAM, address=address[0..13], out=outRAM);
    // screen part of the memory:
    Screen(in=in, load=toScreen, address=address[0..12], out=outScreen);
    // keyboard part of the memory:
    Keyboard(out=outKeyboard);

    // output is a mux depended on the given values we calculated before
    // (a=b=outRAM becuase its 16K and we look at address[13..14])
    // the ouput is either one of out from the ram or screen or keyboard, and given to the out
    Mux4Way16(a=outRAM, b=outRAM, c=outScreen, d=outKeyboard, sel=address[13..14], out=out);


}