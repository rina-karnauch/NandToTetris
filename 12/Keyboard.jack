// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/Keyboard.jack

/**
 * A library for handling user input from the keyboard.
 */
class Keyboard {

    static Array keyboard;
    static char new_line;
    static char back_space;

    /** Initializes the keyboard. */
    function void init() {

        let keyboard = 24576;
        let new_line = 128;
        let back_space = 129;
        return;
    } 

    /**
     * Returns the character of the currently pressed key on the keyboard;
     * if no key is currently pressed, returns 0.
     *
     * Recognizes all ASCII characters, as well as the following keys:
     * new line = 128 = String.newline()
     * backspace = 129 = String.backspace()
     * left arrow = 130
     * up arrow = 131
     * right arrow = 132
     * down arrow = 133
     * home = 134
     * End = 135
     * page up = 136
     * page down = 137
     * insert = 138
     * delete = 139
     * ESC = 140
     * F1 - F12 = 141 - 152
     */
    function char keyPressed() {

        var int value;
        let value = keyboard[0];
        return value;

    }

    /**								
     * Waits until a key is pressed on the keyboard and released,
     * then echoes the key to the screen, and returns the character 
     * of the pressed key.
     */
    function char readChar() {

        var char c;

        while(Keyboard.keyPressed() = 0){
            // waiting to read
        }

        let c = Keyboard.keyPressed();

        while(~(Keyboard.keyPressed() = 0)){
            // waiting to read
        }

        do Output.printChar(c);
        do Output.backSpace();

        return c;
    }

    /**								
     * Displays the message on the screen, reads from the keyboard the entered
     * text until a newline character is detected, echoes the text to the screen,
     * and returns its value. Also handles user backspaces.
     */
    function String readLine(String message) {

        var String string;
        var char current_char;
        var int i;

        do Output.printString(message);

        let string = String.new(64);
        let i = 1;

        while(i > 0){
            let current_char = Keyboard.readChar();
            if(current_char = new_line){
                do Output.println();
                let i = 0;
            }
            else{
                if(current_char = back_space){
                    do string.eraseLastChar();
                }
                else{
                    let string = string.appendChar(current_char);
                }
            }
        }

        return string;
    }   

    /**								
     * Displays the message on the screen, reads from the keyboard the entered
     * text until a newline character is detected, echoes the text to the screen,
     * and returns its integer value (until the first non-digit character in the
     * entered text is detected). Also handles user backspaces. 
     */
    function int readInt(String message) {

        var String string;
        var int i;

        let string = Keyboard.readLine(message);

        let i = string.intValue();
        return i;
    }
}
